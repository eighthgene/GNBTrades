buildscript {

    apply from: "dependencies.gradle"

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.gradlePlugin}"
        classpath classpaths.kotlin
        classpath classpaths.saveArgs
        classpath classpaths.kotlinAndroidExt
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

subprojects {

    if (project.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'androidx.navigation.safeargs.kotlin'
    apply plugin: 'kotlin-android-extensions'

    android {

        compileSdkVersion configs.compileSdk
        buildToolsVersion configs.buildTools

        defaultConfig {

            minSdkVersion configs.minSdk
            targetSdkVersion configs.targetSdk

            if (project.name == 'app') {
                applicationVariants.all { variant ->
                    variant.outputs.all {
                        outputFileName = "app-${variant.name}-${variant.versionName}.apk"
                    }
                }
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8.toString()
        }

    }

    dependencies {

        // Kotlin
        implementation libs.kotlin

        // Koin
        api libs.koin
        api libs.koinViewModel
        api libs.koinScope


        // Coroutines
        implementation libs.coroutinesCore
        implementation libs.coroutinesAndroid

        // Logging
        implementation libs.timber

        // Unit tests
        testImplementation testLibs.junit

        androidTestImplementation testLibs.androidTest
        androidTestImplementation testLibs.androidTestRunner
        androidTestImplementation testLibs.espresso
        androidTestImplementation testLibs.androidTestCore
        debugImplementation testLibs.androidTestFragment

    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}
